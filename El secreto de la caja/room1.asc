// room script file


function room_Load()
{
  if(HasPlayerBeenInRoom(2)){
    oSunnyWindow.Visible = true;
  }
  if(player.PreviousRoom != 6){
    bedroomMusic.Play();
  }
}

function room_FirstLoad()
{
  Wait(40);
  player.Say("Menuda pesadilla");
}

function room_AfterFadeIn()
{
  if(player.PreviousRoom == 5){
    Verbs.MovePlayer(625, 790);
  }
}

function hBedroomExit_Mode8()
{
  bedroomMusic.Stop();
  player.EnterRoom(5, 1075, 209, eDirectionDown);
}

function oCigarretes_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Asesino!");
 }
 else if(Verbs.UsedAction(eGA_PickUp)) {
    Verbs.AnyClickWalkLookPick(1780, 630, eDirectionRight, "Ven aqui, asesino!", oCigarretes.ID , iCigarretes); 
 }
 else Verbs.Unhandled();
}

function oWallet_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Mi cartera");  
 }
 else if(Verbs.UsedAction(eGA_PickUp)) {
    Verbs.AnyClickWalkLookPick(1620, 780, eDirectionDown, "A la saca", oWallet.ID , iWallet); 
 }
 else Verbs.Unhandled();
}

function oLever_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Sistema de seguridad ante una posible invasion alienigena");
 }
 else if(Verbs.UsedAction(eGA_PickUp)) {
    Verbs.AnyClickWalkLookPick(1169, 369, eDirectionRight, "Seguro que me vendra bien", oLever.ID , iLever); 
 }
 else Verbs.Unhandled();
}

function oMirror_AnyClick()
{
  if(Verbs.UsedAction(eGA_LookAt) || Verbs.UsedAction(eGA_Use)) {
  Verbs.MovePlayer(1320, 450);
  player.FaceDirection(eDirectionUp);
  player.Say("Toy to bueno!");
 }
 else if(Verbs.UsedAction(eGA_Push)) {
   if(mirrorPosition == 1){
      player.Say("No quiero empujarlo mas");
   }
   else{
     Verbs.MovePlayer( 1150, 360);
     Wait(10);
     oMirror.X = oMirror.X + 70;
     mirrorPosition = mirrorPosition + 1;
   }
 }
 else if(Verbs.UsedAction(eGA_Pull)) {
    if(mirrorPosition == -1){
      player.Say("No quiero tirar de el mas");
    }
    else{
      Verbs.MovePlayer(1150, 360);
      Wait(10);
      oMirror.X = oMirror.X - 70;
      mirrorPosition = mirrorPosition -1;
    }
 }
 else Verbs.Unhandled();
}

function oClosetOpened_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Tengo que ir de compras");
 }
 else if(Verbs.UsedAction(eGA_Close)) {
    Verbs.MovePlayer( 1520, 520);
    Wait(10);
    if(oCoat.Visible){
      oCoat.Visible = false;
    }
    oClosetOpened.Visible = false;
 }
 else Verbs.Unhandled();
}

function hBedroomCloset_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("El armario de la ropa");
 }
 else if(Verbs.UsedAction(eGA_Open)) {
   dElena.SetOptionState(4, eOptionOn);
   Verbs.MovePlayer( 1520, 520);
   if(hasBedroomClosetBeenOpenedWithKey){
      Wait(10);
      oClosetOpened.Visible = true;
      if(!player.HasInventory(iCoat)){
        oCoat.Visible = true;
       }
   }
   else{
      player.Say("Esta cerrado con llave");
   }
 }
 else Verbs.Unhandled();
}

function oCoat_AnyClick()
{
  if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Mi cazadora, negra, obviamente");
 }
 else if(Verbs.UsedAction(eGA_PickUp)) {
   Verbs.AnyClickWalkLookPick(1500, 495, eDirectionRight, "Listo para salir de aventuras", oCoat.ID , iCoat); 
    oCoat.Visible = false;
 }
 else Verbs.Unhandled();
}

function hBedroomLock_AnyClick()
{
if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("No recuerdo cuando pusimos cerradura al armario");
 }
 else if(Verbs.UsedAction(eGA_Open)) {
   dElena.SetOptionState(4, eOptionOn);
   Verbs.MovePlayer( 1520, 520);
   Wait(10);
   if(hasBedroomClosetBeenOpenedWithKey){
      player.Say("La cerradura ya esta desbloqueada");
   }
   else{
      player.Say("Necesito la llave");
   }
 }
 else if(Verbs.UsedAction(eGA_UseInv) && player.ActiveInventory == iClosetKey){
   Verbs.MovePlayer( 1520, 520);
    Wait(10);
    if(hasBedroomClosetBeenOpenedWithKey){
        player.Say("La cerradura ya esta desbloqueada");
    }
    else{
        hasBedroomClosetBeenOpenedWithKey = true;
        player.Say("La Cerradura ahora esta desbloqueada");
    }
 }
 else Verbs.Unhandled();
}

function hBedroomWallPlug_AnyClick()
{
if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Un enchufe, que esperabas?");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("Mejor no meter los dedos ahi");
 }
 else if(Verbs.UsedAction(eGA_UseInv) && player.ActiveInventory == iCharger){
    Verbs.MovePlayer( 510,690);
    Wait(10);
    player.LoseInventory(iCharger);
    oCharger.Visible = true;
 }
 else Verbs.Unhandled();
}

function oCharger_AnyClick()
{
if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Once euros que me han sablao");
 }
 else if(Verbs.UsedAction(eGA_UseInv) && player.ActiveInventory == iMobile){
   if(isMobileCharged){
      player.Say("Ya esta cargado, deberia ver leer el mensaje");
   }
   else{
      Verbs.MovePlayer(510,690);
      Wait(10);
      player.LoseInventory(iMobile);
      oBedroomMobile.Visible = true;
      isMobileCharging = true;
      player.Say("Eso es, venga esos vatios ahi, que no paren");
   }
 }
 else Verbs.Unhandled();
}

function oBedroomMobile_AnyClick()
{
  if(Verbs.UsedAction(eGA_LookAt)) {
    if(isMobileCharged){
      player.Say("Esta cargado!");
    }
    else{
      player.Say("Esta cargando");
    }
 }
 else if(Verbs.UsedAction(eGA_PickUp)) {
   if(isMobileCharged){
     Verbs.AnyClickWalkLookPick(1500, 495, eDirectionLeft, "Me sentia desnudo sin el", oBedroomMobile.ID , iMobile); 
   }
   else{
     player.Say("Todavia se esta cargando");
   }
 }
 else Verbs.Unhandled();
}

function hBedroomPictures_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Son fotos, si");
 }
 else Verbs.Unhandled();
}

function hBedroomHeater_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Muy util en invierno");
 }
 else Verbs.Unhandled();
}

function hBedroomSwitch_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Interruptor!");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("No es hora de dormir");
 }
 else Verbs.Unhandled();
}

function hBedroomTv_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Muy rica la tele");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("Tengo cosas mejores que hacer");
 }
 else Verbs.Unhandled();
}

function hBedroomBed_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Bendita invencion");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("He dormido ya bastante");
 }
 else Verbs.Unhandled();
}

function hBedroomLamp_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Dan luz si las enciendes");
  Wait(10);
  player.Say("Magia");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("No necesito mas luz");
 }
 else Verbs.Unhandled();
}

function hBedroomWindow_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
   player.Say("Esta nevando!");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("No estoy tan mal como para tirarme por la ventana");
 }
 else Verbs.Unhandled();
}

function hBedroomCurtains_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("mmm, hubiera dicho que teniamos un estor");
 }
 else Verbs.Unhandled();
}

function hBedroomGalan_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Yo si que soy un galan");
 }
 else Verbs.Unhandled();
}

function oSunnyWindow_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("El cambio climatico no existe!");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("No estoy tan mal como para tirarme por la ventana");
 }
 else Verbs.Unhandled();
}

