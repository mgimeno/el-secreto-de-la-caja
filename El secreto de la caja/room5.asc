// room script file

int moveElenasHandCounter = 0;

function room_Load()
{
  if(player.PreviousRoom == 1){
    oLivingRoomDoorOpened.Visible = false;
  }

  livingRoomMusic.Play();
  
  if(hasNeedToSwapWaterGlassWithEmptyGlass){
      hasNeedToSwapWaterGlassWithEmptyGlass = false;
      oLivingRoomWaterGlass.Visible = false;
      oLivingRoomEmptyGlass.Visible = true;
  }
}

function closeDoor(){
  Verbs.MovePlayer(1155,525);
  player.FaceObject(oLivingRoomDoorOpened);
  Wait(10);
  oLivingRoomDoorOpened.Visible = false;
}

function room_AfterFadeIn()
{
  if(player.PreviousRoom == 1){
    Verbs.MovePlayer(1070, 300);
  }
  else if(player.PreviousRoom == 2){
    closeDoor();
  }
}

function room_FirstLoad()
{
  Verbs.MovePlayer(1165, 700);
  cElena.Say("Siestecica de pijama y orinal?");
  player.Say("Me he quedado traspuesto");
  cElena.Say("Mira a ver tu movil");
  cElena.Say("Lleva un buen rato sonando");
  player.Say("Vale");
}

function repeatedly_execute() 
{
  if(hasNeedToImmediatelyShowLivingRoomWaterGlass){
      hasNeedToImmediatelyShowLivingRoomWaterGlass = false;
      oLivingRoomWaterGlass.Visible = true;
  }
  
  moveElenasHandCounter++;
  if(moveElenasHandCounter > 130){
      moveElenasHandCounter = 0;
      oElenaArm1.Graphic = 12;
  }
  else if(moveElenasHandCounter > 110){
      oElenaArm1.Graphic = 11;
  }
}

function hLivingRoomBedroomExit_Mode8()
{
  livingRoomMusic.Stop();
  player.EnterRoom(1, 155, 840, eDirectionRight);
}

function oLivingRoomDoorOpened_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("La puerta a lo desconocido");
 }
 else if(Verbs.UsedAction(eGA_Close)) {
   closeDoor();
 }
 else if(Verbs.UsedAction(eGA_WalkTo)){
    if(!HasPlayerBeenInRoom(2)){
      player.Say("La verdad esta ahi fuera");
    }
    Verbs.MovePlayer(1215, 458);
    livingRoomMusic.Stop();
    player.EnterRoom(2, 680, 1027, eDirectionDown);
 }
 else Verbs.Unhandled();
}

function hLivingRoomDoorClosed_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
    player.Say("La puerta a lo desconocido");
 }
 else if(Verbs.UsedAction(eGA_Open)) {
   if(hasNeedToGoOutFromHome){
     if(player.HasInventory(iCoat)){
       Verbs.MovePlayer(1155,525);
       oLivingRoomDoorOpened.Visible = true;
     }
     else{
        player.Say("Necesito ir al badulaque pero con la que esta cayendo, no voy a salir sin abrigo");
     }
   }
   else{
      player.Say("No tengo ninguna necesidad de salir de casa");
      if(!player.HasInventory(iCoat)){
        player.Say("A parte hace mucho frio");
      }
   }
   
 }
 else Verbs.Unhandled();
}

function oLivingRoomMobile_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("El movil que NO me compre en Nueva York");
 }
 else if(Verbs.UsedAction(eGA_PickUp)) {
    Verbs.AnyClickWalkLookPick(250, 810, eDirectionRight, "Tengo varios mensajes", oLivingRoomMobile.ID , iMobile);

 }
 else Verbs.Unhandled();
}

function oRemoteControl_AnyClick()
{
  if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("El poder!");
 }
 else if(Verbs.UsedAction(eGA_PickUp)) {
    Verbs.AnyClickWalkLookPick(625, 680, eDirectionDown, "Tengo el poder!", oRemoteControl.ID , iRemoteControl);
    cElena.Say("De Grayskull");
 }
 else Verbs.Unhandled();
}

function oLivingRoomClock_AnyClick()
{
  if(Verbs.UsedAction(eGA_LookAt)) {
    if(hasNeedToFixClock){
      player.Say("Las manecillas no se mueven");
    }
    else if(hasPutBatteriesOnClock){
      player.Say("Functiona!");
    }
    else{
      player.Say("Las diez");
    }
 }
 else if(hasNeedToFixClock && Verbs.UsedAction(eGA_PickUp)) {
   Verbs.AnyClickWalkLookPick(1454, 606, eDirectionUp, "Menos mal que estos pantacas tienen los bolsillos grandes", oLivingRoomClock.ID , iClock); 
 }
 else Verbs.Unhandled();
}

function oLivingRoomWaterGlass_AnyClick()
{
if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Medio lleno o medio vacio?");
 }
 else Verbs.Unhandled();
}

function oLivingRoomEmptyGlass_AnyClick()
{
if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Hay que beber mucha agua al dia, claro que si");
 }
 else if(canPickupEmptyGlass && Verbs.UsedAction(eGA_PickUp)) {
   Verbs.AnyClickWalkLookPick(1217, 800, eDirectionRight, "Me lo llevo", oLivingRoomEmptyGlass.ID , iEmptyGlass); 
 }
 else Verbs.Unhandled();
}

function oLivingRoomTvMatch_AnyClick()
{
  if(Verbs.UsedAction(eGA_LookAt)) {
    player.Say("Partidazo!");
    player.Say("Me quedaria a verlo pero tengo cosas que hacer");
   }
   else if(Verbs.UsedAction(eGA_UseInv) && player.ActiveInventory == iRemoteControl){
    if(areBatteriesOutFromRemoteControl){
        player.Say("No funciona");
        player.Say("Quiza que le haya sacado las pilas tiene algo que ver");
    }
    else{
      Verbs.MovePlayer(685, 585);
      oLivingRoomTvMatch.Visible = false;
    }
 }
   else Verbs.Unhandled();
}

function hLivingRoomTvOff_AnyClick()
{
if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("La caja tonta");
 }
 else if(Verbs.UsedAction(eGA_UseInv) && player.ActiveInventory == iRemoteControl){
    if(areBatteriesOutFromRemoteControl){
        player.Say("No funciona");
        player.Say("Quiza que le haya sacado las pilas tiene algo que ver");
    }
    else{
      Verbs.MovePlayer(685, 585);
      oLivingRoomTvMatch.Visible = true;
      Wait(30);
      cElena.Say("Otra vez futbol...");
      cElena.Say("Colombia Ghana?");
      cJuan.Say("No, van empate");
      Wait(20);
      cElena.Say("Madre mia...");
    }
 }
 else if(Verbs.UsedAction(eGA_Use)) {
  player.Say("Necesitaria el mando de la tele");
 }
 else Verbs.Unhandled();
}

function hClockNail_AnyClick()
{

if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Ahi se sujeta el reloj");
 }
 else if(Verbs.UsedAction(eGA_UseInv) && player.ActiveInventory == iClock){
    Verbs.MovePlayer(1454, 606);
    player.LoseInventory(iClock);
    oLivingRoomClock.Visible = true;
    if(hasPutBatteriesOnClock){
        player.Say("A funcionar");
        player.Say("Menudo genio estoy hecho");
        dElena.SetOptionState(4, eOptionOffForever);
        dElena.SetOptionState(5, eOptionOn);
        
    }
 }
 else Verbs.Unhandled();
}

function hLivingRoomPlatonPicture_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
    player.Say("La escuela de Atenas");
    player.Say("Eso si que era una escuela y no el Joaquin Costa");
 }
 else Verbs.Unhandled();
}

function hComputer_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
    player.Say("Es util pero cuando da problemas lo escacharias contra la pared");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("Ostia!");
    player.Say("Me veo a mi mismo jugando a juego donde aparezco jugando a un juego");
    player.Say("donde aparezco jugando a un juego donde aparezco jugando a juego...");
    player.Say("Que movida");
 }
 else Verbs.Unhandled();
}

function hLivingRoomLamp_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Veo lucecicas!");
 }
 else Verbs.Unhandled();
}

function hPuzzleBox_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt) || Verbs.UsedAction(eGA_Use)) {
    player.Say("Con esta maldita caja empezo todo");
    player.Say("Ahora que si no fuera por Elena, aun estaria ahi sin resolver");
    player.Say("No tengo paciencia yo pa estas cosas");
    cElena.Say("Cuando tienes razon, tienes razon");
 }
 else Verbs.Unhandled();
}

function hDocuments_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("He aqui todos los calculos para la proxima jornada del Hattrick");
  player.Say("Con mi proyecto a largo plazo seguro que subo a segunda");
  player.Say("Arriba Olivas Negras FC!");
 }
 else Verbs.Unhandled();
}

function hSofa_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("La mejor inversion de la casa");
 }
 else if(Verbs.UsedAction(eGA_Use)) {
  player.Say("Ojala, pero tengo cosas que hacer");
 }
 else Verbs.Unhandled();
}

function hUmbrellaStand_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Soy de la opinion que los ingleses no tienen imaginacion para crear palabras");
  player.Say("Luego pienso que nosotros tenemos palabras como paraguas y me tranquilizo");
 }
 else Verbs.Unhandled();
}

function hFruitBowl_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Dicen que hay que comer frutas y verduras 5 veces al dia");
  player.Say("La gente dice unas chorradas...");
  player.Say("A mi dame un buen chuleton sangriento y vete a zurrarte la banana por ahi");
 }
 else Verbs.Unhandled();
}

function hCandle_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Si es que soy un romantico");
 }
 else Verbs.Unhandled();
}

function hWorldMap_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("El mundo es vasto y diverso");
  player.Say("Pero que gustoso se esta en casa");
 }
 else Verbs.Unhandled();
}

function hLivingRoomSwitch_AnyClick()
{
 if(Verbs.UsedAction(eGA_LookAt)) {
  player.Say("Interruptor!");
 }
 else if(Verbs.UsedAction(eGA_Use)){
    player.Say("No quiero arriesgarme a apagar la luz con Kuklinski aqui");
 }
 else Verbs.Unhandled();
}

function region1_WalksOff()
{
   if(oLivingRoomDoorOpened.Visible == true){
      player.StopMoving();
      player.Say("Mejor cierro la puerta");
      closeDoor();
   }
}

function hLivingRoomBedroomExit_AnyClick()
{
  
  if(oLivingRoomDoorOpened.Visible == true && (Region.GetAtRoomXY(player.x, player.y) == region[0])){
      player.StopMoving();
      player.Say("Mejor cierro la puerta");
      closeDoor();
  }
}

